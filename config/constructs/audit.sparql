PREFIX audit: <https://data.archiefpunt.be/_audit/>
PREFIX abv: <https://data.q.archiefpunt.be/>

construct {
    ?change_set a audit:Audit ;
        audit:this_version ?this_version;
        audit:created_date ?created_date;
        audit:subject_of_change ?soc;
        audit:change_reason ?reason;
        audit:creator_name ?name;
        audit:creator_group ?group;
        audit:diff ?diff.
}
WHERE {
    graph audit: {
        select * where {
            {{VALUES}}
            ?change_set ?p ?o ;
                audit:diff ?diff;
                audit:created_date ?created_date;
                audit:subject_of_change ?soc ;
                audit:change_reason ?reason;
                audit:creator_name ?name;
                a audit:ChangeSet .

            optional {
                ?change_set		 audit:preceding_change_set ?pcs.
            }

            optional {
                ?change_set	 audit:other_data ?data.
            }

            optional {
                ?change_set audit:creator_group ?group.
            }

            bind(substr(str(?change_set), strlen(str(audit:change_sets))+2 ) as ?version_id)
            bind(replace(substr(str(?soc), strlen(str(abv:))+1), '(.*)/(.*)', '$1') as ?entity)
            bind(replace(substr(str(?soc), strlen(str(abv:))+1), '(.*)/(.*)', '$2') as ?id)
            bind(uri(concat(substr(str(?change_set), 0, strlen(str(audit:change_sets))-11),'_logic/version?', 'id=', ?id, '&version_id=', ?version_id, '&entity=', ?entity)) as ?this_version)
        }  order by desc(?created_date)
    }
}